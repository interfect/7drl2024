root ::= RoguelikeObject
RoguelikeObject ::= (
    "{" line
    "\"type\": " q itemType q and
    "\"rarity\": " q rarityLevel q and
    "\"quality\": " q qualityLevel q and
    "\"cursed\": " boolean and
    "\"domain\": " q elementalDomain q and
    "\"name\": " title and
    "\"symbol\": " char and
    "\"health\":" ws percent "\n"
    "}"
)

line ::= "\n" "    "
and ::= "," line

itemType ::= "weapon" | "armor" | "food" | "wand" | "potion" | "monster"

rarityLevel ::= "common" | "uncommon" | "rare" | "ultra-rare" | "legendary"

qualityLevel ::= "low" | "medium" | "high"

elementalDomain ::= "metal" | "war" | "earth" | "air" | "fire" | "water" | "life" | "death" | "wood" | "business"

thisIsSentence ::= q "This is " quotedsentence q

q ::= "\""

ws ::= [ \t\n]*
string ::= q ([^"\\]*) q
char ::= q quotedchar q
quotedchar ::= [-a-zA-Z0-9.,/#!$%^&*;:{}=_`~()@+?><[\]+'] | "\\\"" | "\\\\"
sentence ::= q quotedsentence q
quotedsentence ::= ([^".!?]*) [.?!]
title ::= q titleword q | q titleword " " titletail q
titletail ::= titleword | titlemiddleword " " titletail | titleword " " titletail
titleword ::= [A-Z] [a-z]*
titlemiddleword ::= ([A-Z] [a-z]*) | "of" | "the" | "with" | "for"
boolean ::= "true" | "false"
number ::= [0-9]+ "."? [0-9]*
int ::= [0-9]+
percent ::= "100" | [1-9][0-9] | [0-9]
stringlist ::= "[" ws "]" | "[" ws string ("," ws string)* ws "]"
numberlist ::= "[" ws "]" | "[" ws number ("," ws number)* ws "]"
intlist ::= "[" ws "]" | "[" ws int ("," ws int)* ws "]"
